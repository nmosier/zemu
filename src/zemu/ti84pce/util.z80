#ifndef TI84PCE_UTIL_Z80
#define TI84PCE_UTIL_Z80

#include "util.inc"

;; upperbyte -- get upper byte of long
;; INPUTS:
;;  hl = long value
;; OUTPUTS:
;;  a = upper byte
;; ERRORS: no.
;; DESTROYS: (none)
;; TODO: Shouldn't use global mem.
;; *** portable ***
upperbyte:
   ld (upperbyte.long),hl
   ld a,(upperbyte.long+2)
   ret
upperbyte.long: .dl 0

;; djb2_putc -- add char to existing cksum
;; INPUTS:
;;  hl = cksum
;;  a = new char
;; OUTPUTS:
;;  hl = new cksum
;; DESTROYS: a
djb2_putc:
  push af
   push bc
    push de
     ld de,33
     push af
     call mul_hl_de
     pop af
     ld de,0
     ld e,a
     add hl,de
    pop de
   pop bc
  pop af
  ret

;; djb2_unputc -- remove char from existing cksum
;; INPUTS:
;;  hl = cksum
;;  a = char to remove
;; OUTPUTS:
;;  hl = new cksum
;; DESTROYS: ???
djb2_unputc:
  push af
   push bc
    push de
     ld de,0
     ld e,a
     or a,a
     sbc hl,de
     ld de,33
     call div_hl_de
    pop de
   pop bc
  pop af
  ret


;; wtols -- word to long, signed
;; INPUTS:
;;  hl[15:0] = 16-bit word, signed
;; OUTPUTS:
;;  hl = sign-extended from hl[15:0]
;; DESTROYS: (none)
;; *** portable ***
wtols:
   push de
   ld de,0
   bit 7,h
   jr z,wtols.transfer
   dec de ; de <- $FFFFFF
wtols.transfer:
   ld d,h
   ld e,l
wtols.done:
   ex de,hl
   pop de
   ret


;; wtolu -- word to long, unsigned
;; INPUTS:
;;  hl[15:0] = 16-bit word, unsigned
;; OUTPUTS:
;;  hl = 24-bit unsigned long from hl[15:0]
;; DESTROYS: (none)
;; *** portable ***
wtolu:
   push de
   ld de,0
   ld d,h
   ld e,l
   ex de,hl
   pop de
   ret


#endif