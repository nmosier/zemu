; zaddr.z80 -- routines for zaddresses
#ifndef ZADDR_Z80
#define ZADDR_Z80

#include "zemu.inc"
#include "zhdr.inc"
#include "zutil.inc"
#include "zaddr.inc"

;; zaddr_valid -- check whether address is valid
;; INPUTS:
;;  hl = Z-address
;;  a = access mask
;;  ix = pointer to zmap header
;; OUTPUTS: (none)
;; ERRORS: yes (ZERR_BADADDR).
;; DESTROYS: de
zaddr_valid:
   bit ZADDR_WRITE,a
   jr nz,zaddr_valid.write
zaddr_valid.read:
   ld de,(ix+ZMAP_HDR_STORYSIZE)
zaddr_valid.cmp:
   call cp_hl_de
   ccf
   ret nc
   ld a,ZERR_BADADDR
   ret

zaddr_valid.write:
   ld de,(ix+ZMAP_HDR_STATIC)
   jr zaddr_valid.cmp

;; zaddr_page -- get page number of Z-address
;; INPUTS:
;;  hl = Z-address
;;  ix = pointer to zmap header
;; OUTPUTS:
;;  a = page number
;; ERRORS: yes.
;; DESTROYS: a, bc
zaddr_pageno:
   ;; get mask of page number (only high byte)
   ld a,(ix+ZMAP_HDR_PAGEMASK)
   cpl
   and a,h
   ld c,a
   ;; get upper byte of address -> a
   call upperbyte
   ;; rotate byte
   ld b,(ix+ZMAP_HDR_PAGEBITS)
   inc b
   or a,a   ; NC for loop
   jr zaddr_pageno.entry
zaddr_pageno.shift:
   rra
   rr c
zaddr_pageno.entry:
   ;; INVARIANT: always NC
   djnz zaddr_pageno.shift
   ;; Once loop is completed, A should be 0.
   ld a,c
zaddr_pageno.done:
   ;; NOTE: due to previous loop, always NC, so SUCCESS.
   ret

;; zaddr_offset -- get offset of address within page
;; INPUTS:
;;  hl = Z-address
;;  ix = pointer to zmap header
;; OUTPUTS:
;;  de = offset
;; DESTROYS: a
;; ERRORS: no, but always NC.
zaddr_offset:
   ld de,0
   ld e,l   ; pages are always multiples of lower bit
   ld a,(ix+ZMAP_HDR_PAGEMASK)
   and a,h
   ld d,a
   ret

;; zaddr_pageent -- get address of page entry
;; INPUTS:
;;  a = page number
;; OUTPUTS:
;;  ix = pointer to page entry
;; DESTROYS: de
;; ERRORS: no (but always NC)
zaddr_pageent:
   MLT_A(ZMAP_TABENTLEN, R_DE)
   ld ix,(zmap_table)
   add ix,de ; pointer to table entry
   ret

#if 0
;; zaddr_accesspage -- get pointer to access page with given permissions
;; INPUTS:
;;  a = page number
;;  b = access mode (ZADDR_READ, ZADDR_WRITE)
;; OUTPUTS:
;;  hl = pointer to page
;; ERRORS: yes.
;; DESTROYS: ???
zaddr_accesspage:
   call zaddr_pageent
   bit ZADDR_WRITE,b
   jr nz,zaddr_accesspage.write

zaddr_accesspage.write:
   ;; check if address in dynamic memory
   bit ZMAP_ENT_FLAGS_COPY,(ix+ZMAP_ENT_FLAGS)
   call nz,zmap_copypage
#endif

;; zaddr_to_paddr -- get physical byte address of Z-machine byte address
;; INPUTS:
;;  hl = Z-address
;;  a = access mode
;; OUTPUTS:
;;  hl = physical address
;; ERRORS: yes.
;; DESTROYS: (all)
;; NOTE: Only access the single byte pointed to by the physical address.
zaddr_to_paddr:
   push ix
   ld ix,(zmap_header)
   call zaddr_valid           ; check if Z-address is valid, given access mode
   push hl                    ; preserve Z-address
   ld d,a                     ; access mode
   call nc,zaddr_pageno       ; get page number -> a
   ld b,d                     ; access mode
   call nc,zmap_fetchpage     ; get page physical address -> hl
   ld ix,(zmap_header)        ; refresh header pointer
   ex (sp),hl                 ; restore Z-address & preserve physical page address
   call nc,zaddr_offset       ; get page offset -> de
   pop hl                     ; restore physical page address
   pop ix
   ret c                      ; ERROR
   add hl,de                  ; compute physical byte address
   ret                        ; SUCCESS (always NC)

;; zaddr_fetch -- fetch byte at logical address
;; INPUTS:
;;  hl = zaddr (logical address)
;; OUTPUTS:
;;  a = byte at zaddr
;;  hl <- hl + 1
;; ERROR: yes.
;; DESTROYS: everything but de
zaddr_fetch:
   push de
   push hl
   ld a,MASK(ZADDR_READ)
   call zaddr_to_paddr
   ex de,hl
   pop hl
   jr c,zaddr_fetch.cleanup
   ld a,(de) ; load byte
   inc hl  ; update ptr
   ; C reset (nc)
zaddr_fetch.cleanup:
   pop de
   ret

;; zaddr_fetch_c -- C wrapper for zaddr_fetch
;; ARGS:
;;  arg1 = uint24_t address
;; RETV: uint8_t byte = *address
;; ERRORS: no.
;; DESTROYS: (all)
zaddr_fetch_c:
   ; push ix
   push iy
   ld iy,0
   add iy,sp
   ld hl,(iy+9) ; address
   push iy
   call zaddr_fetch
   pop iy
   jr nc,_ ; error, but can't propogate
   xor a,a  
_  ld hl,0
   ld l,a
   ld sp,iy
   pop iy
   ; pop ix
   ret


;; zaddr_fetchw -- fetch big-endian word at logical address
;; INPUTS:
;;  hl = logical address
;; OUTPUTS:
;;  hl <- hl + 2
;;  de = word, sign-extended
;; ERRORS: yes.
;; DESTROYS: (all)
zaddr_fetchw:
   call zaddr_fetch
   ret c
   ld d,a
   call zaddr_fetch
   ret c
   ld e,a
   push hl
   ex de,hl
   call wtols ; upcast
   ex de,hl
   pop hl
   scf \ ccf
   ret

;; zaddr_fetchwu -- fetch unsigned word at logical address
zaddr_fetchwu:
   call zaddr_fetch
   ret c
   ld d,a
   call zaddr_fetch
   ret c
   ld e,a
   push hl
   ex de,hl
   call wtolu ; upcast
   ex de,hl
   pop hl
   scf \ ccf
   ret

;; zaddr_fetchb -- fetch byte at logical address and sign-extend
;; INPUTS:
;;  hl = logical address
;; OUTPUTS:
;;  hl <- hl + 1
;;  de = byte, sign-extended
;; ERRORS: yes.
;; DESTROYS: (all)
zaddr_fetchb:
   call zaddr_fetch
   ret c
   ex de,hl
   call btols
   ex de,hl
   scf \ ccf
   ret

;; zaddr_store -- store byte at logical address
;; INPUTS:
;;  hl = zaddr (logical address)
;;  a = byte to store at zaddr
;; OUTPUTS:
;;  hl <- hl + 1
;; ERROR: yes.
;; DESTROYS: everything but de
zaddr_store:
   push de
   push hl
   push af
   ld a,MASK(ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc ; restore byte to store (in b)
   ex de,hl
   pop hl
   jr c,zaddr_fetch.cleanup
   ld a,b
   ld (de),a ; store byte
   inc hl ; update ptr
   ; C reset (nc)
zaddr_store.cleanup:
   pop de
   ret


;; zaddr_storew -- store word at logical address
;; INPUTS:
;;  hl = logical address
;;  de = word to store
;; OUTPUTS:
;;  hl <- hl + 2
;; ERRORS: yes.
;; DESTROYS: all but de
zaddr_storew:
   ld a,d
   call zaddr_store
   ret c
   ld a,e
   jp zaddr_store

;; zaddr_unpack -- convert packed address to byte address
;; INPUTS:
;;  hl = packed address
;; OUTPUTS:
;;  hl = byte address
;; ERRORS: no.
;; DESTROYS: (none)
zaddr_unpack:
   add hl,hl
   ret


;; zaddr_fetchn -- fetch n bytes
;; INPUTS:
;;  hl = logical address
;;  de = destination buffer
;;  b = count
;; OUTPUTS:
;;  hl <- hl + b
;;  de = destination buffer
;; ERRORS:
;; DESTROYS:
zaddr_fetchn:
   push de
   or a,a ; NC
   inc b \ dec b
   jr z,zaddr_fetchn.done ; nothing to do
zaddr_fetchn.loop:
   push bc
   call zaddr_fetch ; fetch byte at logical address hl
   pop bc
   jr c,zaddr_fetchn.done
   ld (de),a
   inc de
   djnz zaddr_fetchn.loop
zaddr_fetchn.done:
   pop de
   ret


;; zaddr_set -- set bit
;; zaddr_res -- reset bit
;; zaddr_bit -- test bit
;; INPUTS:
;;  hl = address
;;  a = bit number
;; OUTPUTS:
;;  ZF set properly (zaddr_bit)
;; DESTROYS: (all)
;; ERRORS: yes.
zaddr_set:
   call maskb
   push af
   ld a,(1 << ZADDR_READ) | (1 << ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc
   ret c
   ld a,b
   or a,(hl)
   ld (hl),a
   ret

zaddr_res:
   call maskb
   push af
   ld a,(1 << ZADDR_READ) | (1 << ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc
   ret c
   ld a,b
   cpl
   and a,(hl)
   ld (hl),a
   ret

zaddr_bit:
   call maskb
   push af
   ld a,MASK(ZADDR_READ)
   call zaddr_to_paddr
   pop bc
   ret c
   and a,(hl)
   ret

#include "zmap.z80"

#endif