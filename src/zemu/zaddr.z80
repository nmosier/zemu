; zaddr.z80 -- routines for zaddresses
#ifndef ZADDR_Z80
#define ZADDR_Z80

#include "zemu.inc"
#include "zhdr.inc"
#include "zutil.inc"
#include "zaddr.inc"

; Note on zaddrs:
; Z-machine V3 story files are at most 128 KB in size. This means that high mem addresses must be
; represented in 17 bits. The Z80 has a word size of 16 bits and the eZ80 has a word size of 24
; bits, so the TI-84+ CE can represent the size in a multibyte register while the TI-83+ family
; can't.
;
; In the future, it'd be nice to support versions above v3, so the most forward-compatible approach
; would be to use another register as the upper address byte on the TI-83+. For symmetry in the ABI,
; all TI-84+ CE zaddr_* routines should also accept the same parameter; however, they should ignore
; it and continue to use the 24-bit long registers for addresses.

;; zaddr_valid -- check whether address is valid
;; INPUTS:
;;  [b:hl] = Z-address
;;  a = access mask
;;  ix = pointer to zmap header
;; OUTPUTS: (none)
;; ERRORS: yes (ZERR_BADADDR).
;; DESTROYS: de
;; *** portable ***
zaddr_valid:
   push bc
   push de
   bit ZADDR_WRITE,a
   jr nz,zaddr_valid.write
zaddr_valid.read:
   LD_CDE_FROM_IX_ZA(ZMAP_HDR_STORYSIZE)
zaddr_valid.cmp:
   call zaddr_cp
   pop de
   pop bc
   ccf
   ret nc
   ld a,ZERR_BADADDR
   ret
zaddr_valid.write:
   LD_CDE_FROM_IX_ZAW(ZMAP_HDR_STATIC)
   jr zaddr_valid.cmp

;; zaddr_pageno -- get page number of Z-address
;; INPUTS:
;;  [b:hl] = Z-address
;;  ix = pointer to zmap header
;; OUTPUTS:
;;  a = page number
;; ERRORS: yes.
;; DESTROYS: (none)
;; *** portable ***
zaddr_pageno:
  push bc
   ;; get mask of page number (only high byte)
   ld a,(ix+ZMAP_HDR_PAGEMASK)
   cpl
   and a,h
   ld c,a
   ;; get upper byte of address -> a
#if TI83PLUS
   ld a,b
#elif TI84PCE
   call upperbyte
#endif
   ;; rotate byte
   ld b,(ix+ZMAP_HDR_PAGEBITS)
   inc b
   or a,a   ; NC for loop
   jr zaddr_pageno.entry
zaddr_pageno.shift:
   rra
   rr c
zaddr_pageno.entry:
   ;; INVARIANT: always NC
   djnz zaddr_pageno.shift
   ;; Once loop is completed, A should be 0.
   ld a,c
zaddr_pageno.done:
   ;; NOTE: due to previous loop, always NC, so SUCCESS.
  pop bc
  ret

;; zaddr_offset -- get offset of address within page
;; INPUTS:
;;  [h:l] = Z-address, lower 16 bits
;;  ix = pointer to zmap header
;; OUTPUTS:
;;  de = offset
;; DESTROYS: a
;; ERRORS: no, but always NC.
;; *** portable ***
zaddr_offset:
  UCAST_L_TO_DE ; pages are always multiples of lower bit
  ld a,(ix+ZMAP_HDR_PAGEMASK)
  and a,h
  ld d,a
  ret

;; zaddr_pageent -- get address of page entry
;; INPUTS:
;;  a = page number
;; OUTPUTS:
;;  ix = pointer to page entry
;; DESTROYS: de
;; ERRORS: yes.
;; *** portable ***
zaddr_pageent:
  MLT_A(SIZEOF_ZMAP_ENT, R_DE)
  call zmap_table
  ret c
  add ix,de ; pointer to table entry
  ret


;; zaddr_to_paddr
;; INPUTS:
;;  [b:hl] = Z-address
;;  a = access mode
;; OUTPUTS:
;;  [a:hl] = generalized address
;; ERRORS: yes. 
zaddr_to_paddr:
  push ix
   ld c,a                  ; save access mode
   call zmap_header
   call nc,zaddr_valid
   call nc,zaddr_offset
   call nc,zaddr_pageno
   push de
    ld b,c                 ; restore access mode
    call nc,zmap_fetchpage
   pop hl
   call nc,file_addr
  pop ix
  ret


;; zaddr_fetch -- fetch byte at logical address
;; INPUTS:
;;  [b:hl] = zaddr (logical address)
;; OUTPUTS:
;;  a = byte at zaddr
;;  [b:hl] <- [b:hl] + 1
;; ERROR: yes.
;; DESTROYS: (none)
;; *** portable ***
zaddr_fetch:
  push bc
   push de
    PUSH_BHL_ZA
     ld a,MASK(ZADDR_READ)
     call zaddr_to_paddr
     ex de,hl
    POP_BHL_ZA
    jr c,zaddr_fetch.cleanup
    ld a,(de) ; load byte
    INC_BHL_ZA ; inc z-addr in [b:hl]
    ; C reset (nc)
zaddr_fetch.cleanup:
   pop de
   ;; restore %c
   ex (sp),hl
   ld c,l
  pop hl
  ret





;; zaddr_fetchw -- fetch big-endian word at logical address
;; INPUTS:
;;  [b:hl] = logical address
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 2
;;  de = word, sign-extended
;; ERRORS: yes.
;; DESTROYS: (none)
;; *** portable ***
zaddr_fetchw:
   push af
   call zaddr_fetch
   ld d,a
   call nc,zaddr_fetch
   jr c,zaddr_fetch.err
   ld e,a
   pop af
   ex de,hl
   WTOLS_HL ; upcast
   ex de,hl
   or a,a ; NC
   ret
zaddr_fetch.err:
   pop hl
   ret

;; zaddr_fetchwu -- fetch unsigned word at logical address
;; INPUTS:
;;  [b:hl] = Z-addr
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 2
;;  de = word, zero-extended
;; ERRORS: yes.
;; DESTROYS: (none)
;; *** portable ***
#if TI83PLUS
zaddr_fetchwu .equ zaddr_fetchw
#elif TI84PCE
zaddr_fetchwu:
   push af
   call zaddr_fetch
   ld d,a
   call nc,zaddr_fetch
   ret c
   ld e,a
   ex (sp),hl
   ld a,h
   pop hl
   ex de,hl
   call wtolu ; upcast
   ex de,hl
   or a,a ; NC
   ret
#endif

;; zaddr_fetchz -- fetch byte Z-address at Z-address
;; INPUTS:
;;  [b:hl] = Z-addr
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 2
;;  [c:de] = Z-addr at [b:hl]
;; ERRORS: yes.
;; DESTROYS: a
#if TI83PLUS
zaddr_fetchz:
  call zaddr_fetchwu
  ld c,0
  ret
#elif TI84PCE
zaddr_fetchz .equ zaddr_fetchwu
#endif


;; zaddr_fetchb -- fetch byte at logical address and sign-extend
;; INPUTS:
;;  [b:hl] = Z-addr
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 1
;;  de = byte, sign-extended
;; ERRORS: yes.
;; DESTROYS: (all)
;; *** portable ***
zaddr_fetchb:
   call zaddr_fetch
   ret c
   ex de,hl
   call btols
   ex de,hl
   or a,a ; NC
   ret

;; zaddr_store -- store byte at logical address
;; INPUTS:
;;  [b:hl] = zaddr (logical address)
;;  a = byte to store at zaddr
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 1
;; ERROR: yes.
;; DESTROYS: a
;; *** portable ***
zaddr_store:
   push bc
   push hl
   push de
   push af
   ld a,MASK(ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc ; restore byte to store (in b)
   jr c,zaddr_store.cleanup
   ld (hl),b ; store byte
   ex de,hl
zaddr_store.cleanup:
   pop de
   pop hl
   pop bc
   ret c
   INC_BHL_ZA
   ret


;; zaddr_storew -- store word at logical address
;; INPUTS:
;;  [b:hl] = logical address
;;  de = word to store
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + 2
;; ERRORS: yes.
;; DESTROYS: a
zaddr_storew:
   ld a,d
   call zaddr_store
   ret c
   ld a,e
   jp zaddr_store

;; zaddr_unpack -- convert packed address to byte address
;; INPUTS:
;;  hl = packed address
;; OUTPUTS:
;;  [b:hl] = byte address
;; ERRORS: no.
;; DESTROYS: (none)
;; *** portable ***
zaddr_unpack:
#if TI83PLUS
   add hl,hl
   ld b,0
   rlc b
#elif TI84PCE
   add hl,hl
#endif
   ret


;; zaddr_fetchn -- fetch n bytes
;; INPUTS:
;;  [b:hl] = logical address
;;  de = destination buffer
;;  a = count
;; OUTPUTS:
;;  [b:hl] <- [b:hl] + a
;;  de = destination buffer
;; ERRORS: yes.
;; DESTROYS: ac
zaddr_fetchn:
   or a,a
   ret z ; nothing to do
   push de
zaddr_fetchn.loop:
   push af
   call zaddr_fetch ; fetch byte at logical address [b:hl]
   jr c,zaddr_fetchn.err
   ld (de),a
   inc de
   pop af
   dec a
   jr nz,zaddr_fetchn.loop
   pop de
   ret
zaddr_fetchn.err:
   pop de
   pop de
   ret

;; zaddr_set -- set bit
;; zaddr_res -- reset bit
;; zaddr_bit -- test bit
;; INPUTS:
;;  [b:hl] = Z-address
;;  a = bit number
;; OUTPUTS:
;;  ZF set properly (zaddr_bit)
;; DESTROYS: (all)
;; ERRORS: yes.
;; *** portable ***
zaddr_set:
   call maskb
   push af
   ld a,(1 << ZADDR_READ) | (1 << ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc
   ret c
   ld a,b
   or a,(hl)
   ld (hl),a
   ret

zaddr_res:
   call maskb
   push af
   ld a,(1 << ZADDR_READ) | (1 << ZADDR_WRITE)
   call zaddr_to_paddr
   pop bc
   ret c
   ld a,b
   cpl
   and a,(hl)
   ld (hl),a
   ret

zaddr_bit:
   call maskb
   push af
   ld a,MASK(ZADDR_READ)
   call zaddr_to_paddr
   pop bc
   ret c
   and a,(hl)
   ret

#if TI83PLUS
#include "ti83plus/zaddr.z80"
#elif TI84PCE
#include "ti84pce/zaddr.z80"
#endif

#include "zmap.z80"
#include "util.z80"

#endif
