; zbranch.z80 -- branch & jump utility routines

#ifndef ZBRANCH_Z80
#define ZBRANCH_Z80

#include "zaddr.inc"
#include "zutil.inc"

;; zexec_cjmp -- execute conditional branches (jumps)
;; INPUTS:
;;  a = predicate's truth value ($00 = false, $FF = true)
;;  zframe_pc points to branch bytes within instruction
;; OUTPUTS:
;;  zframe_pc updated (in yes-branch and no-branch cases)
;; DESTROYS: (all)
;; ERRORS: yes.
zexec_cjmp:
   push af ; save truth value
   call zframe_pc_get
   call zcode_decode_branch
   ex (sp),hl
   ld a,h ; restore truth value
   pop hl
   ret c
   ; apply branch inversion flag
   jr nz,zexec_cjmp.inv
   cpl ; branch when false
zexec_cjmp.inv:
   ; [b:hl] = no-branch PC
   ; de = raw offset
   or a,a
   jr z,zexec_cjmp.store_pc ; don't take the branch
   ex de,hl
#if TI83PLUS
   ld c,b
#endif
   ; [c:de] = no-branch PC
   ; hl = raw offset
   push de
   ld de,1
   call cp_hl_de
   pop de
   jp c,zexec.rfalse ; offset == 0 => RFALSE
   jp z,zexec.rtrue  ; offset == 1 => RTRUE
zexec_cjmp.jump:
   ; otherwise, apply offset
   UCAST_HL_TO_BHL_ZA
   ADD_BHL_CDE_ZA
   DEC_BHL_ZA
   DEC_BHL_ZA ; don't forget to subtract 2!
   ; hl = new PC
zexec_cjmp.store_pc:
   call zframe_pc_set
   or a,a
   ret

#endif