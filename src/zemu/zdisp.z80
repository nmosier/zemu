;; zdisp.z80 -- display routines

#include "zdisp.inc"
#include "zti.inc"

#if TI84PCE
#include "ti84pce/zdisp.z80"
#endif
#if TI83PLUS
#include "ti83plus/zdisp.z80"
#endif

;; sfont_width -- get width of small font character
;; INPUTS:
;;  a = character
;; OUTPUTS:
;;  bc = width of character
;; DESTROYS: (none)
;; ERRORS: no.
sfont_width:
   push hl
   push de
   MLT_A(SFONT_TAB_ENTLEN, R_HL)
   SYSCALL(_SFont_Len)
#if TI84PCE
   push af
   ld a,b
   UCAST_A_TO_BC
   pop af
#elif TI83PLUS
   ld c,b
   ld b,0
#endif
   pop de
   pop hl
   ret


;; zdisp_rowaddr -- address of row in VRAM
;; INPUTS:
;;  a = row number
;; OUTPUTS:
;;  hl = row address
;; DESTROYS: a, de
;; ERRORS: no.
;; *** portable ***
zdisp_rowaddr:
   MLT_A_DISP_WIDTH(R_HL)
   ld de,DISP_VRAM
   add hl,de
   ret


;; zdisp_pixaddr -- address of pixel in VRAM
;; INPUTS:
;;  a = row number
;;  hl = column number
;; OUTPUTS:
;;  hl = pixel address
;; DESTROYS: a, de
;; ERRORS: no.
;; *** portable ***
zdisp_pixaddr:
#if TI84PCE
   add hl,hl ; x2 because there are 2 bytes per pixel
#endif
   push hl
   call zdisp_rowaddr
   pop de
   add hl,de
   ret




;; zdisp_scrollrect_up -- shift rectangle upwards in VRAM
;; INPUTS:
;;  ix = rectangle
;;  a = upward shift (in pixels)
;; OUTPUTS: ???
;; ERRORS: no.
;; *** portable ***
zdisp_scrollrect_up:
   call zdisp_scrollrect_up.copy
   call zdisp_scrollrect_up.clear
   ret



;; zdisp_scrollrect_up.clear -- clear rectangle after scrolling up
;; INPUTS:
;;  ix = rectangle
;;  a = upward shift (in pixels)
;; OUTPUTS:
;;  ix = rectangle
;;  a = upward shift
;; ERRORS: no.
;; DESTROYS: (all)
;; *** portable ***
zdisp_scrollrect_up.clear:
   ;; clear lower rectangle that will be exposed by scroll
   ;; initialize clear-rect
   push af
   push ix
   LD_HL_IX
   ld ix,-ZRECT_SIZE
   add ix,sp
   ld sp,ix
   LD_DE_IX
   ld bc,ZRECT_SIZE
   ldir
   ld b,a
   ld c,(ix+ZRECT.HEIGHT)
   ;; clear-rect.height <- min(upward-shift, rect-height)
   cp a,c   ; flags(shift - rect.height)
   jr c,_
   ld a,c
_  ld (ix+ZRECT.HEIGHT),a  ; save clear-rect.height
   ;; clear-rect.orgrow <- rect.orgrow + rect.height - clear-rect.height
   neg
   add a,c
   add a,(ix+ZRECT.ORGROW)
   ld (ix+ZRECT.ORGROW),a  ; save clear-rect.orgrow
   ;; NOTE: orgcol and width stay the same.
   call zdisp_clearrect
   LEA_HL_IX(ZRECT_SIZE)
   ld sp,hl
   pop ix
   pop af
   ret


;; zdisp_scrollrect_up.copy -- scroll up by copying contents of VRAM
;; INPUTS:
;;  ix = rectangle
;;  a = upward shift
;; OUTPUTS:
;;  ix = rectangle
;;  a = upward shift
;; ERRORS: no.
;; DESTROYS: ???
zdisp_scrollrect_up.copy:
   push af
   ld b,(ix+ZRECT.HEIGHT)
   ld c,a
   sub a,b
   jr nc,zdisp_scrollrect_up.copy.done  ; nothing to copy
   neg
   ld b,a
   ;; b stores number of rows to copy (b > 0)
   push bc
   ;; compute src and dst addresses
   ;;  compute dst address
   ld a,(ix+ZRECT.ORGROW)
   LD_HLL_FROM_IX(ZRECT.ORGCOL)
   call zdisp_pixaddr
   push hl
   ;;  compute src address
   add a,c  ;  add upward shift
   LD_HLL_FROM_IX(ZRECT.ORGCOL)
   call zdisp_pixaddr
   push hl
   jr zdisp_scrollrect_up.copy.entry
zdisp_scrollrect_up.copy.loop:
   push bc
   LD_BCL_FROM_IX(ZRECT.WIDTH)
   sla c
   rl b
   or a,a
   sbc hl,bc
   ex de,hl
   sbc hl,bc
   ld bc,DISP_WIDTH*PIXEL_SIZE
   add hl,bc
   ex de,hl
   add hl,bc
   push de
   push hl
zdisp_scrollrect_up.copy.entry:
   pop hl   ; src
   pop de   ; dst
   LD_BCL_FROM_IX(ZRECT.WIDTH)
   sla c
   rl b
   ldir
   pop bc
   djnz zdisp_scrollrect_up.copy.loop
zdisp_scrollrect_up.copy.done:
   pop af
   ret


