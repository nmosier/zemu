;; zmap.z80 -- zmap routines
;; Nicholas Mosier 2019
#ifndef ZMAP_Z80
#define ZMAP_Z80

#include "zaddr.inc"
#include "zemu.inc"


;; zmap_fetchpage -- fetch pointer to zpage.
;; INPUTS:
;;  a = page number
;;  b = access mode (ZADDR_READ, ZADDR_WRITE)
;; OUTPUT:
;;  ix = file pointer
;; DESTROYS: (all)
;; ERRORS: yes.
;; NOTE: performs no validation of page number.
;; TODO: this should check if INRAM flag of page for writes.
zmap_fetchpage:
  call zaddr_pageent
  ret c
  bit ZADDR_WRITE,b
  ret z
  bit ZMAP_ENT.FLAGS.COPY,(ix+ZMAP_ENT.FLAGS)
  ret z
  ld hl,zemu_pagepfx
  push af
   call zmap_copypage
  pop bc
  ld a,b
  call nc,zaddr_pageent
  ret c
  ld (ix+ZMAP_ENT.FLAGS),MASK(ZMAP_ENT.FLAGS.SAVE)
  LEA_IX_IX(ZMAP_ENT.FILE)
  jp file_to_RAM



;; zmap_copypage -- copy page (used when write to page is read-only)
;; INPUTS:
;;  a = page number
;;  hl = name prefix
;; OUTPUTS: (none)
;; DESTROYS: (all)
;; ERRORS: yes.
;; NOTE: Doesn't touch zmap entry flags. Caller must set them properly.
;; *** portable ***
zmap_copypage:
  ;; get src and dst var names
  ;; name page
  ld (zmap_copypage.pageno),a
  ld de,zmap_copypage.file + FILE.NAME
  call zmap_namepage
  push ix
   push iy
    ld hl,zmap_copypage.getfile ; getter for source
    ld iy,zmap_copypage.file
    call file_copy
   pop iy
   call nc,zmap_copypage.getfile
   jp c,err1
   ld hl,zmap_copypage.file
   LEA_DE_IX(ZMAP_ENT.FILE) ; flags preserved
   ld bc,SIZEOF_FILE
   ; ld (ix+ZMAP_ENT.FLAGS),MASK(ZMAP_ENT.FLAGS.SAVE)
  pop ix
  ldir
  ret


zmap_copypage.getfile:
  ld a,(zmap_copypage.pageno)
  push de
   call zaddr_pageent
  pop de
  ret c
  LEA_IX_IX(ZMAP_ENT.FILE)
  ret

zmap_copypage.pageno: .db 0

zmap_copypage.file: RES_FILE

zemu_pagepfx: .db "ZEMUP",0

;; zmap_namepage -- rename page after being copied
;; INPUTS:
;;  hl = prefix
;;  a = page ID
;;  de = buffer (at least 8 chars)
;; OUTPUTS:
;;  hl = buffer
;; DESTROYS: a, bc, de
;; ERRORS: no.
;; *** portable ***
zmap_namepage:
   push de     ; buffer
   push af     ; ID
   ld bc,VARNAMELEN-2
   call strncpy
   pop af      ; ID
   ex de,hl
   ld b,a
   and a,$f0
   rrca
   rrca
   rrca
   rrca
   call xtoc
   ld (hl),a
   inc hl
   ld a,b
   and a,$0f
   call xtoc
   ld (hl),a
   inc hl
   ex de,hl
   ld hl,0
   call cp_hl_bc
   ex de,hl
   jr z,_
   ld (hl),0
_  pop hl
   ret

;; zmap_load -- load saved zmap into working zmap
;; INPUTS:
;;  hl = name of saved zmap (zmap_load)
;; OUTPUTS:
;;  touches & resizes working zmap and copies contents of saved zmap into it
;; DESTROYS: (all)
;; ERRORS: yes.
;; *** portable ***
zmap_load:
  ld de,zmap_canonical_file+FILE.NAME
  ld bc,VARNAMELEN
  call strncpy
  push ix
   ld ix,zmap_canonical_file
   push iy
    ld iy,zmap_working_file
    call file_open
    ld hl,zmap_load.getfile
    call nc,file_copy
   pop iy
  pop ix
  jp nc,zmap_open
  ret

zmap_load.getfile:
  ld ix,zmap_canonical_file
  or a,a
  ret


;; zmap_open -- open zmap
;; INPUTS: (none)
;; OUTPUTS: (none)
;; DESTROYS: (all)
;; ERRORS: yes.
zmap_open:
  push ix
   call nc,zmap_header
   jr c,zmap_open.done
   ld b,(ix+ZMAP_HDR_NPAGES)
   call zmap_table
   jr c,zmap_open.done
zmap_open.loop:
   LEA_IX_IX(ZMAP_ENT.FILE)
   push bc
    call file_open
   pop bc
   jr c,zmap_open.done
   LEA_IX_IX(SIZEOF_ZMAP_ENT - ZMAP_ENT.FILE)
   djnz zmap_open.loop
   or a,a
zmap_open.done:
  pop ix
  ret


;; zmap_close -- close zmap
;; INPUTS: (none)
;; OUTPUTS: (none)
;; DESTROYS: (all)
;; ERRORS: yes.
;; OPTIM: write function that takes function and iterates over all entries.
zmap_close:
  push ix 
   call zmap_header
   ld b,(ix+ZMAP_HDR_NPAGES)
   call zmap_table
   jr c,zmap_close.done
zmap_close.loop:
   LEA_IX_IX(ZMAP_ENT.FILE)
   push bc
    call file_close
   pop bc
   jr c,zmap_close.done
   LEA_IX_IX(SIZEOF_ZMAP_ENT - ZMAP_ENT.FILE)
   djnz zmap_close.loop
   or a,a
zmap_close.done:
  pop ix
  ret


;; zmap_header -- get pointer to zmap header
;; INPUTS: (none)
;; OUTPUTS:
;;  ix = pointer to zmap header
;; DESTROYS: (none)
;; ERRORS: yes.
;; OPTIM required
zmap_header:
  push hl
   push de
    push bc
     push af
      ld ix,zmap_working_file
      call file_baseaddr
      LD_IX_HL
      call pop_all
  ret


;; zmap_table -- get pointer to zmap table
;; INPUTS: (none)
;; OUTPUTS:
;;  ix = pointer to zmap table
;; DESTROYS: (none)
;; ERRORS: yes.
;; OPTIM required.
zmap_table:
  push hl
   push de
    push bc
     push af
      ld ix,zmap_working_file
      ld hl,ZMAP_HDRSIZE
      call file_addr
      LD_IX_HL
      call pop_all
  ret


;; zmap_get_working_file -- return pointer to working zmap file
;; INPUTS: (none)
;; OUTPUTS:
;;  ix <- zmap_working_file
;; DESTROYS: (none)
;; ERRORS: no, but NC.
zmap_get_working_file:
  ld ix,zmap_working_file
  or a,a
  ret

;;;;;;;;;;;;;;;;;;;;;;;;
;;;;; ZMAP GLOBALS ;;;;;
;;;;;;;;;;;;;;;;;;;;;;;

zmap_working_file: D_FILE("ZEMUMAP", 0)
zmap_canonical_file: RES_FILE
zmap_name_canonical: RESB(VARNAMELEN)
zmap_name_current:   .db "ZEMUMAP",0

#endif