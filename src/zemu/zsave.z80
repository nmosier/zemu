;; zsave.z80 -- routines for saving & restoring game

#include "zemu.inc"
#include "zsave.inc"
#include "zutil.inc"

;; zsave_size -- compute number of bytes required to save current game state
;; INPUTS: (none)
;; OUTPUTS:
;;  hl = size
;; DESTROYS: bc, de
;; ERRORS: no.
;; *** portable ***
zsave_size:
  ;; get size of current zmap
  push ix 
   call zmap_header
   jr c,zsave_size.err1
   UCAST_FROM_IX_TO_BC
  pop ix
  inc hl
  ld b,(hl)
  ;; get size of current zmem
  ld hl,(zframe_sp)
  ;; zmap + zmem
  add hl,bc
  ;; add fixed-sized fields
  ld de,ZFRAME_SIZE+3*WORD_SIZE ; words to store length & size bytes
  add hl,de
  ret

zsave_size.err1:
  pop hl
  ret


;; zsave_savemap -- save a map
;; INPUTS:
;;  ix = zmap pointer
;;  hl = name prefix (recommended: ${SAVENAME}P)
;; OUTPUTS: ???
;; DESTROYS: ???
;; ERRORS: yes.
;; *** portable ***
zsave_savemap:
   ;; frame setup
   ld b,(ix+ZMAP_HDR_NPAGES)  ; pages remaining
   ld c,0                     ; page ID
   ;; Iterate thru table. For each page entry, if flag `SAVE' is set,
   ;; copy the page and rename it using the given name prefix
   ;; move copied page to flash
   ;; change flags: flags <- COPY
   LEA_IX_IX(ZMAP_HDR_END)   ; table ptr -> ix
zsave_savemap.loop:
   ;; INVARIANTS:
   ;;  hl = name prefix
   ;;  b = pages remaining
   ;;  c = page ID
   bit ZMAP_ENT.FLAGS.SAVE,(ix+ZMAP_ENT.FLAGS)
   jr z,_
   
   ;; copy page
   push hl
   push bc
   ld a,c
   call zmap_copypage_ent
   pop bc
   pop hl
   jr c,zsave_savemap.err

   ;; update name of page & put into OP1
   push hl
   push bc
   ld a,c
   LEA_DE_IX(ZMAP_ENT.FILE + FILE.NAME)
   call zmap_namepage
   call appvar_to_OP1

   ;; archive copied page
   push ix
   push iy
   ld iy,flags
   SYSCALL(_Arc_Unarc)
   pop iy
   pop ix
   
   pop bc
   pop hl

   ;; update flags
   ld (ix+ZMAP_ENT.FLAGS),MASK(ZMAP_ENT.FLAGS.COPY)
_  LEA_IX_IX(SIZEOF_ZMAP_ENT)
   inc c
   djnz zsave_savemap.loop
zsave_savemap.leave:
   or a,a   ; SUCCESS
   ret

zsave_savemap.err:
   ;; TODO -- this should delete any variables that were created
   ret

;; zsave_save -- save game
;; INPUTS:
;;  hl = name of save
;; OUTPUTS:
;;  a = whether save was successful
;; ERRORS: yes.
;; DESTROYS: (all)
;; *** portable ***
zsave_save:
  ;; create empty appvar of required size
  push hl           ; preserve name
   push hl
    call zsave_size   ; size -> hl
    LD_BC_HL
   pop hl            ; name -> hl
   call appvar_touch_trunc
   ex de,hl          ; save data ptr -> de
   ;; copy in frame
   ld hl,zframe_begin
   ld bc,ZFRAME_SIZE
   ldir
   ;; find offsets of frame pointers (fp and sp)
   ;; copy in map appvar
   push de    ; save for update
    push ix
     call zmap_header
     jr c,zsave_save.err3
     UCAST_FROM_IX_TO_BC
    pop ix
    inc hl
    ld b,(hl)
    dec hl
    ldir
   pop ix
   ;; update map
  pop hl   ; name
  push de
   call zsave_savemap
  pop de
  ret c    ; ERROR
  ;; copy in mem
  ld bc,(zframe_sp)    ; NOTE: sp as an offset is also size of zmem.
  ld a,c
  ld (de),a
  inc de
  ld a,b
  ld (de),a
  inc de
  call zstack_begin
  ret c
  ldir
zsave_save.done:
  xor a,a
  dec a       ; ld a,$ff && NC -- SUCCESS
  ret

zsave_save.err3:
  pop hl
  pop hl
  pop hl
  ret


;; zstate_restore -- restore game
;; INPUTS:
;;  hl = name of save
;; OUTPUTS: (none)
;; ERRORS: yes.
;; DESTROYS: (all)
;; *** portable ***
zsave_restore:
   call appvar_find
   jr c,zsave_restore.false   ; not found, so false
   ex de,hl
   ;; copy saved frame into current frame
   ld de,zframe_begin
   ld bc,ZFRAME_SIZE
   ldir
   ;; get size of map
   call ldw_bc_from_hl  ; bc.s <- (hl)   
   ;; create/resize working zmap and bring into RAM
   push hl
   push bc
   ld hl,zmap_name_current       ; working zmap name
   call appvar_touch_trunc
   ex de,hl                      ; set destination for copy
   pop bc
   pop hl
   ret c          ; ERROR
   ;; load saved zmap into working zmap
   push de
   ldir
   ex (sp),hl     ; preserve save ptr and restore working zmap pointer
   LD_IX_HL
   LEA_IX_IX(2)
   ; call zmap_markupdate
   pop hl         ; restore save ptr
   ;; get size of zmem
   CLEAR_UPPER_BC
   ld c,(hl)
   inc hl
   ld b,(hl)
   inc hl
   ex de,hl
   ld hl,ZMEM_SIZE
   call cp_hl_bc
   ex de,hl   
   jr c,zsave_restore.stackoverflow    ; exceeds allocated memory for stack
   ;; restore memory
   ex de,hl
   call zstack_begin
   ex de,hl
   ret c
   ldir
zsave_restore.true:
   xor a,a
   dec a       ; ld a,$ff && NC -- SUCCESS
   ret

zsave_restore.stackoverflow:
   ld a,ZERR_STACKOVER
   ret

zsave_restore.false:
   xor a,a
   ret

;; zsave_restart -- restart game
;; INPUTS:
;;  hl = name of canonical zmap
;; OUTPUTS: ???
;; DESTROYS: ???
;; ERRORS: ???
;; *** portable ***
zsave_restart:
   call zmap_load      ; load canonical map into working map
   call nc,zcpu_init   ; re-init CPU
   ret
   ;; TODO -- should actually preserve two bits; see spec.
   ;; TODO -- should delete all pages that are marked as 'SAVE' in the current map.
