;; zvar.z80 -- variable manip routines

#include "zvar.inc"

#if ZDBG
#include "zdbg.inc"
#endif

;; zvar_localaddr -- get address of local variable
;; INPUTS:
;;  a = variable number
;; OUTPUTS:
;;  hl = pointer to local variable (p-addr)
;; DESTROYS: a,de
;; ERRORS: yes. (Checks bounds of var num)
;; *** portable ***
zvar_localaddr:
   ; validate var num
   dec a
   ld hl,zframe_nl
   cp a,(hl)
   ccf
   UCAST_A_TO_HL
   ld a,ZERR_NOLOCL
   ret c
   add hl,hl ; a * ZWORD_SIZE
   ld de,(zframe_fp)
   add hl,de
   ld de,(zstack_begin)
   add hl,de
   or a,a ; NC, success
   ret

;; zvar_globaladdr -- get address of global variable
;; INPUTS:
;;  a = global variable number
;; OUTPUTS:
;;  hl = logical address of global variable
;; ERRORS: yes.
;; DESTROYS: (all)
;; *** portable ***
zvar_globaladdr:
  sub a,ZVAR_GLOBALMIN
  push af
  LD_BHL_IMM(ZHDR_GLOBALS)
  call zaddr_fetchz
  ex de,hl
#if TI83PLUS
  ld b,c
#endif
  pop de
  ret c
  ld a,d
  MLT_A(ZWORD_SIZE, R_DE)
  ADD_BHL_DE_ZA
  or a,a ; NC
  ret


;; zvar_fetch -- fetch variable value.
;; INPUTS:
;;  a = variable number
;; OUTPUTS:
;;  hl = variable value (16-bit)
;; DESTROYS: (all)
;; ERRORS: yes.
;; *** portable ***
zvar_fetch:
   push af
   call zvar_fetch.switch
   pop bc
   ret c
   call wtols ; upcast
#ifdef ZDBG
   push hl
   ld ix,ZDBG_FLAGS
   bit ZDBG_FLAGS_WATCH,(ix)
   ld a,b ; varnum
   or a,a  ; NC -- read
   call nz,zdbg_watch_hook
   pop hl
#endif
   or a,a
   ret

zvar_fetch.switch:
   cp ZVAR_STACK
   jp z,zvar_fetch.stack
   cp ZVAR_GLOBALMIN
   jp c,zvar_fetch.local
   jp zvar_fetch.global

zvar_fetch.stack:
   ; check if stack is empty
   ld de,(zframe_sp)
   ld hl,(zframe_fp)
   call cp_hl_de  ; fp < sp <=> C
   ccf
   ld a,ZERR_STACKEMPTY
   ret c
   ; get value
   ex de,hl
   ld bc,(zstack_begin)
   add hl,bc
   CLEAR_UPPER_DE
   dec hl
   ld e,(hl)
   dec hl
   ld d,(hl)
   or a,a
   sbc hl,bc
   ; update stack ptr
   ld (zframe_sp),hl
   ex de,hl
   or a,a
   ret

zvar_fetch.local:
   ; get addr of local var (on stack)
   call zvar_localaddr
   ret c ; error
   ; load var
   CLEAR_UPPER_DE
   ld d,(hl)
   inc hl
   ld e,(hl)
   ex de,hl
   or a,a
   ret

zvar_fetch.global:
   call zvar_globaladdr
   call nc,zaddr_fetchw
   ex de,hl
   ret

;; zvar_store -- store value to variable
;; INPUTS:
;;  a = variable number
;;  hl = value to store
;; OUTPUTS: (none)
;; ERRORS: yes.
;; DESTROYS: (all)
zvar_store:
#ifdef ZDBG
   ; debug watch hook
   push hl
   ld ix,ZDBG_FLAGS
   bit ZDBG_FLAGS_WATCH,(ix)
   scf
   call nz,zdbg_watch_hook
   pop hl
#endif   
   ; switch on var type
   ex de,hl
   cp ZVAR_STACK
   jp z,zvar_store.stack
   cp ZVAR_GLOBALMIN
   jp c,zvar_store.local
   jp zvar_store.global

zvar_store.stack:
   ld hl,(zframe_sp)
   ld bc,(zstack_begin)
   add hl,bc
   ld (hl),d
   inc hl
   ld (hl),e
   inc hl
   or a,a
   sbc hl,bc
   ld (zframe_sp),hl
   or a,a
   ret

zvar_store.local:
   ; get addr
   push de
   call zvar_localaddr
   pop de
   ret c ; error
   ; store val
   ld (hl),d
   inc hl
   ld (hl),e
   or a,a
   ret ; NC

zvar_store.global:
   push de
   call zvar_globaladdr
   pop de
   jp nc,zaddr_storew
   ret ; C


;; zvar_downcast -- cast unsigned value to 24 bits to 8 bits
;; INPUTS:
;;  hl = 24-bit unsigned value
;; OUTPUTS:
;;  a = 8-bit unsigned value
;; ERRORS: no.
;; DESTROYS: (none)
;; *** portable ***
zvar_downcast:
   ld a,l
   ret


;; zvar_modify -- modify variable in place
;; INPUTS:
;;  a = variable number
;;  hl = pointer to unary function
;; OUTPUTS:
;;  (a) updated
;;  hl = new value
;; ERRORS: yes.
;; DESTROYS: (all), ix
;; *** portable ***
zvar_modify:
   push af
   push hl
   call zvar_fetch
   pop ix
   call nc,zvar_modify.jump
   pop bc
   ld a,b
   push hl
   call nc,zvar_store
   pop hl
   ret

zvar_modify.jump:
   jp (ix)


;; zvar_write -- read variable number from memory and then store value to that variable
;; INPUTS:
;;  hl = value to store
;;  zframe_pc set
;; OUTPUTS:
;;  zframe_pc updated
;; DESTROYS: (all)
zvar_write:
   call zcode_readvarnum
   jp nc,zvar_store
   ret ; C
