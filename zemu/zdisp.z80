;; zdisp.z80 -- display routines

#include "zdisp.inc"

;; sysputc -- wrapper for putc system entry point
;; INPUTS:
;;  a = ASCII char to print
;; OUTPUTS: (none)
;; DESTROYS: (none)
;; ERRORS: no.
sysputc:
   push de
   push ix
   push bc
   ex de,hl
   ld ix,penCol
   ld l,(ix+0)
   ld h,(ix+1)
   call sfont_width
   ld b,0
   add.s hl,bc
   ld bc,DISP_WIDTH+1
   sbc hl,bc
   ex de,hl
   call nc,sysnewline
   call _VPutMap
sysputc.done:
   pop bc
   pop ix
   pop de
   ld a,b
   ret

;; sysnewline -- wrapper for newline system entry point
;; INPUTS: (none)
;; OUTPUTS: (none)
;; DESTROYS: (none)
;; ERRORS: no.
sysnewline:
   push af
   ld a,(penRow)
   add a,SFONT_HEIGHT
   ld (penRow),a
   xor a,a
   ld (penCol),a
   ld (penCol+1),a
   pop af
   ret   

;; sfont_width -- get width of small font character
;; INPUTS:
;;  a = character
;; OUTPUTS:
;;  c = width of character
;; DESTROYS: (none)
;; ERRORS: no.
sfont_width:
   push hl
   push de
   ld hl,SFONT_TAB_ADDR
   ld de,SFONT_TAB_ENTLEN
   ld d,a
   mlt de
   add hl,de
   ld c,(hl)
   pop de
   pop hl
   ret


;; sys_clrcurrow -- clear cursor row
;; INPUTS:
;;  a = cursor row (0 - 10?)
;; OUTPUTS: (none)
;; DESTROYS: a, b, de
;; ERRORS: no.
sys_clrcurrow:
   rlca
   rlca
   rlca ; a *= 8
   add a,$80
   jp _ClearRow
   