#include "ti84pce.inc"
.assume ADL=1
.org userMem-2

VARNAMELEN .equ 8
ZMAP_TABENTLEN .equ (VARNAMELEN + 1 + 3) ; this should be manually updated

ZMAP_HDRSIZE .equ 6
ZMAP_HDR_PAGESIZE .equ 0
ZMAP_HDR_STORYSIZE .equ ZMAP_HDR_PAGESIZE + 2
ZMAP_HDR_NPAGES .equ ZMAP_HDR_STORYSIZE + 3

ZMAP_ENT_VARNAME .equ 0
ZMAP_ENT_FLAGS .equ ZMAP_ENT_VARNAME + VARNAMELEN
ZMAP_ENT_PTR .equ ZMAP_ENT_FLAGS + 1

ZMAP_ENT_FLAGS_INRAM .equ 0

.db tExtTok, tAsm84CeCmp

start:
   ; clear screen & reset cursor
   call _ClrScrnFull
   ld hl,0
   ld.s (curRow),hl
   ld hl,zork_zmap
   call zemu_open ; open story file
   jp c,zemu_perror
   call zmem_init ; initialize memory
   jp c,zemu_perror
   ret

; zemu_open -- open zemu story file
; INPUTS:
;  - hl = pointer to name of zmap appvar
; OUTPUTS:
;  zemu_appvar, zemu_header, zemu_table set
; DESTROYS: (all)
zemu_open:
zemu_open.find_zmap:
   call appvar_to_RAM
   ld a,ZERR_NOVAR
   ret c
   ; store zmap pointers
   ex de,hl
   ld (zmap_appvar),hl      ; store zmap ptr
   inc hl
   inc hl
   ld (zmap_header),hl
   ld de,ZMAP_HDRSIZE
   add hl,de         ; pointer to table
   ld (zmap_table),hl
zemu_open.table_init:
   ld ix,(zmap_header)
   ld b,(ix+ZMAP_HDR_NPAGES)
   ld ix,(zmap_table)
zemu_open.table_init.loop:
   ; move varname into OP1
   push ix
   push.s bc
   lea hl,ix+ZMAP_ENT_VARNAME
   call appvar_to_OP1
   call _ChkFindSym
   pop.s bc
   pop ix
   ld a,ZERR_NOVAR
   ret c  ; return if no page found
   ld (ix+ZMAP_ENT_FLAGS),0
   call _ChkInRAM
   jr nz,zemu_open.table_init.loop_end
   ; set RAM flag
   set ZMAP_ENT_FLAGS_INRAM,(ix+ZMAP_ENT_FLAGS)
   ; save pointer
   inc de
   inc de
   ld (ix+ZMAP_ENT_PTR),de
   ;;; DEBUG ;;;
   lea hl,ix+ZMAP_ENT_VARNAME
   push ix
   push bc
   call _PutS
   call _NewLine
   pop bc
   pop ix
   ;;; DEBUG ;;;
zemu_open.table_init.loop_end
   lea ix,ix+ZMAP_TABENTLEN
   ; loop condition
   djnz zemu_open.table_init.loop
   scf \ ccf ; success
   ret
   
;; zemu_cleanup -- cleanup in preparation for exit
zemu_cleanup:
   ; check if zmap was found
   ld hl,(zmap_appvar)
   ld de,0
   sbc hl,de ; (hl == 0)
   jr z,zemu_cleanup.table_loop.end ; skip table cleanup if zmap not loaded
   ld ix,(zmap_header)
   ld b,(ix+ZMAP_HDR_NPAGES)
   ld ix,(zmap_table)
zemu_cleanup.table_loop:
   ; if zpage is in RAM, archive it
   bit ZMAP_ENT_FLAGS_INRAM,(ix+ZMAP_ENT_FLAGS)
   jr z,zemu_cleanup.table_loop.cond
   ; move varname into OP1
   lea hl,ix+ZMAP_ENT_VARNAME
   call appvar_to_OP1
   call _Arc_Unarc ; archive zpage
zemu_cleanup.table_loop.cond:
   lea ix,ix+ZMAP_TABENTLEN
   djnz zemu_cleanup.table_loop
zemu_cleanup.table_loop.end:
zemu_cleanup.done:
zemu_exit.done:
   scf \ ccf ; success
   ret

zmap_appvar: .dl 0
zmap_header: .dl 0
zmap_table:  .dl 0

;; TESTING ;;
zork_zmap: .db "ZORK",0,0,0,0

#include "zerr.z80"
#include "zutil.z80"
#include "zmem.z80"