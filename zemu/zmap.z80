;; zmap.z80 -- zmap routines
;; Nicholas Mosier 2019

#include "zaddr.inc"

;; zmap_fetchpage -- fetch pointer to zpage.
;; INPUTS:
;;  a = page number
;;  b = access mode (ZADDR_READ, ZADDR_WRITE)
;; OUTPUT:
;;  hl = pointer to zpage in RAM (skips over size bytes)
;; DESTROYS: (all)
;; ERRORS: yes.
;; NOTE: performs no validation of page number.
zmap_fetchpage:
   call zaddr_pageent
zmap_fetchpage_ent:
#if 1
   bit ZADDR_WRITE,b
   jr z,_
   bit ZMAP_ENT_FLAGS_COPY,(ix+ZMAP_ENT_FLAGS)
   call nz,zmap_copypage_ent
_  ld hl,(ix+ZMAP_ENT_PTR)
   or a,a
   ret
#else
   bit ZMAP_ENT_FLAGS_COPY,(ix+ZMAP_ENT_FLAGS)
   jp nz,zmap_copypage_ent
   ld hl,(ix+ZMAP_ENT_PTR)
   or a,a
   ret
#endif

;; zmap_copypage -- copy page (used when write to page is read-only)
;; zmap_copypage_ent -- copy page, given pointer to entry
;; INPUTS:
;;  a = page number (zmap_copypage)
;;  ix = pointer to page entry in zmap (zmap_copypage_ent)
;; OUTPUTS:
;;  hl = pointer to zpage in RAM (skips over size bytes)
;;  current zmap updated
;; DESTROYS: ???
;; ERRORS: yes.
zmap_copypage:
   ;; get pointer to entry
   ld ix,(zmap_table)
   ld de,ZMAP_TABENTLEN
   ld d,a
   mlt de
   add ix,de   ; pointer to table entry
zmap_copypage_ent:
   ;; get src and dst var names
   lea hl,ix+ZMAP_ENT_VARNAME
   ld de,zmap_copypage.buf
   ld bc,VARNAMELEN
   push hl
   call strncpy
   pop de
   ;; rename page
   ld hl,zmap_copypage.buf
   call zmap_renamepage
   ex de,hl
   ;; copy appvar
   push ix
   call appvar_copy
   pop ix
   ret c
   inc hl   ; skip size bytes
   inc hl 
   ;; update zmap entry
   res ZMAP_ENT_FLAGS_COPY,(ix+ZMAP_ENT_FLAGS)
   set ZMAP_ENT_FLAGS_INRAM,(ix+ZMAP_ENT_FLAGS)
   ld (ix+ZMAP_ENT_PTR),hl
   ex de,hl
   lea hl,ix+ZMAP_ENT_VARNAME
   call zmap_renamepage
   ex de,hl ; hl <- ptr
   or a,a
   ret
zmap_copypage.buf: RESB(VARNAMELEN)

;; zmap_renamepage -- rename page after being copied
;; INPUTS:
;;  hl = previous name
;; OUTPUTS:
;;  name modified in-place
;; DESTROYS: a, bc
;; ERRORS: no.
zmap_renamepage:
   push hl
   xor a,a
   ld bc,VARNAMELEN
   cpir
   jr nz,_
   dec hl
_  dec hl
   dec hl
   dec hl
   ld (hl),'W'
   pop hl
   ret