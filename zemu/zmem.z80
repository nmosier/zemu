;; zmem.z80 -- memory management for zemu & save states
;; TODO -- stack overflow?


ZMEM_SIZE .equ $2000

;; GLOBAL VARIABLES ;;

;; NOTE: stack grows up.
zstack_begin: .dl 0

zframe_begin:
zframe_fp: .dl 0
zframe_sp: .dl 0
zframe_pc: .dl 0
zframe_nl: .db 0
zframe_rv: .db 0
zframe_end:

ZFRAME_SIZE .equ zframe_end - zframe_begin
;;

;; zmem_init -- initialize memory for zemu. Empty call state and program stack.
;; INPUTS: (none)
;; OUTPUTS:
;;  Sets zmem_appvar, zmem_callstate_top, zmem_stack_top.
;; ERROR: a = error code, CF set
;; DESTROYS: (all), OP1
;; TODO: check size bytes of appvar.
zmem_init:
   ; get appvar in RAM
   ld hl,zmem_name
   ld de,ZMEM_SIZE+2 ; +2 for size bytes
   call appvar_touch
   ret c
   ex de,hl
   inc hl ; skip size bytes
   inc hl
   ld (zstack_begin),hl
   ld (zframe_fp),hl
   ld (zframe_sp),hl
   xor a,a
   ld (zframe_nl),a
   ld hl,$ffffff
   ld (zframe_pc),hl
   ret
   

zmem_name: .db "ZEMUM",0

;; zstack_enter -- enter new stack frame
;; INPUTS:
;;  a = number of local variables
;; OUTPUTS: (none)
;; DESTROYS: hl,de,ix
;; ERRORS: (none)
zstack_enter:
   push bc ; save variable number
   ; push new stack frame onto stack
   ld hl,zframe_begin
   ld de,(zframe_sp)
   ld bc,ZFRAME_SIZE
   ldir
   ; de == (zframe_sp) + ZFRAME_SIZE
   ; update frame pointer
   ld (zframe_fp),de
   ; update stack pointer
   ld hl,$000002
   ld h,a
   mlt hl
   add hl,de
   ld (zframe_sp),hl
   ; update number of locals (NL)
   ld (zframe_nl),a
   ; update return variable
   pop bc ; restore variable number
   ret   


;; zstack_leave -- leave current stack frame
;; INPUTS: (none)
;; OUTPUTS: (none)
;; DESTROYS: hl,bc,de,ix
;; ERRORS: yes.
zstack_leave:
   ; restore stack frame
   ld ix,(zframe_fp)
   lea hl,ix-ZFRAME_SIZE
   ld de,zframe_begin
   ld bc,ZFRAME_SIZE
   ldir
   or a,a
   ret


;; zframe_sp_base -- get base of stack pointer
;; INPUTS: (none)
;; OUTPUTS:
;;  hl = pointer to s.p. base
;; ERRORS: no.
;; DESTROYS: (none)
zframe_sp_base:
   push de
   ld hl,0
   ld a,(zframe_nl)
   ld l,a
   add hl,hl
   ld de,(zframe_fp)
   add hl,de
   pop de
   ret
